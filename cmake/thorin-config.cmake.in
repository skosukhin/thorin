# Provides Thorin library and include path.
# Once done this will define
#
# Thorin_INCLUDE_DIRS (including dependencies to LLVM/RV/Half)
# Thorin_LIBRARIES (including dependencies to LLVM/RV)
# Thorin_FOUND
# Thorin_HAS_LLVM_SUPPORT | Thorin_HAS_RV_SUPPORT
# AnyDSL_LLVM_LINK_SHARED

@PACKAGE_INIT@

include("${CMAKE_CURRENT_LIST_DIR}/thorin-targets.cmake")

set(Thorin_LIBRARIES AnyDSL::thorin)
set(Thorin_HAS_LLVM_SUPPORT @LLVM_FOUND@)
set(Thorin_HAS_RV_SUPPORT @RV_FOUND@)
set(AnyDSL_LLVM_LINK_SHARED @AnyDSL_LLVM_LINK_SHARED@)
set(AnyDSL_LLVM_DIR "@LLVM_DIR@")

get_target_property(Thorin_INCLUDE_DIRS ${Thorin_LIBRARIES} INTERFACE_INCLUDE_DIRECTORIES)

if(Thorin_HAS_LLVM_SUPPORT)
    find_package(LLVM REQUIRED CONFIG PATHS "@LLVM_DIR@" NO_DEFAULT_PATH)
    # The command above reports on finding ZLIB, LibXml2, etc. but is silent
    # regarding LLVM itself, which is a bit misleading. Therefore, we report
    # explicitly:
    message(STATUS "Found LLVM: ${LLVM_DIR} (found version \"${LLVM_PACKAGE_VERSION}\")")
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(Thorin DEFAULT_MSG Thorin_DIR)
